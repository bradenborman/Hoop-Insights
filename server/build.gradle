plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.0'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.hoopinsights'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

tasks.named('jar') {
	enabled = false
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
//	implementation 'org.springframework.jdbc:spring-jdbc'
	runtimeOnly 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('copyClient') {
	dependsOn ':client:clientBuild'

	doFirst {
		delete 'src/main/resources/static'
		delete 'src/main/resources/templates'
	}

	doLast {
		copy {
			from project(':client').file('build')
			into 'src/main/resources/static'
		}
		copy {
			from project(':client').file('build/index.html')
			into 'src/main/resources/templates'
		}
	}
}

tasks.named('processResources') {
	dependsOn 'copyClient'
}

tasks.named('clean') {
	dependsOn ':client:clientClean'
}

tasks.register('stage') {
	dependsOn 'build'
	dependsOn 'clean'
}